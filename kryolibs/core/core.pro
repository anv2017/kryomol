# File generated by kdevelop's qmake manager. 
# ------------------------------------------- 
# Subdir relative project main directory: ./molecule
# Target is a library:
configfile = ../config/qryomol.pri
include($${configfile} )

TEMPLATE = lib

CONFIG += warn_on qt static

!static {
DEFINES += QRYOMOLCOREDLL
}


TARGET = qryomolcore


HEADERS += atom.h bond.h \
           coordinate.h \
           couplingconstant.h quantumcoupling.h \
           energy.h \
           molecule.h \
	   frame.h \
           frequency.h \
           quantumplot.h \
           sinusoid.h \
           kryomolcore_export.h \
           thermo.h coreexport.h  ringperceptor.h \
           pdbtools.h \
           chemicalshift.h \
           threshold.h \
           animation.h \
           orbitaldata.h \
           transitionchange.h \
           orbital.h \
           electronicdensity.h \
           grid.h \
           renderdensity.h \
    basiscenter.h

SOURCES += atom.cpp bond.cpp \
           coordinate.cpp \
           energy.cpp \
           quantumplot.cpp \
           sinusoid.cpp \
           molecule.cpp quantumcoupling.cpp  frame.cpp \
           thermo.cpp couplingconstant.cpp ringperceptor.cpp \
           pdbtools.cpp \
           chemicalshift.cpp \
           animation.cpp \
           orbitaldata.cpp \
           transitionchange.cpp \
           orbital.cpp \
           electronicdensity.cpp \
           grid.cpp \
           renderdensity.cpp \
    basiscenter.cpp

INCLUDEPATH += ../tools \ 
../tools ../plugin ../3dparty/qwt6/src


cuda {
INCLUDEPATH += ../cudatools
}

win32 {
    DLLDESTDIR = "$$(KRYOMOL_DIR)/bin"
    CONFIG(debug,debug|release) {
    LIBS += -L../tools/debug
    }
    CONFIG(release,debug|release) {
    LIBS += -L../tools/release
    }
}

unix {
  LIBS += -L../tools
}

unix {
!macx {
  mkl {
  LIBS += -lmkl_lapack -lmkl_ia32 -lguide
  }
  lapack {
  LIBS += -llapack -lblas
  }
} else {
  LIBS += -framework accelerate
}

}

win32 { 

    LIBS +=  mkl_sequential.lib mkl_intel_c.lib mkl_core.lib
}

headers.files = $$HEADERS
!macx {
    headers.path = "$$(KRYOMOL_DIR)/include/kryomol"
    target.path = $$(KRYOMOL_DIR)/lib
}else {
    QMAKE_LFLAGS_SONAME  = -Wl,-install_name,@executable_path/../lib/
    headers.path = $$(KRYOMOL_DIR)/$${APPNAME}.app/Contents/Headers/kryomol
    target.path = $$(KRYOMOL_DIR)/$${APPNAME}.app/Contents/lib
}
#INSTALLS += headers target
