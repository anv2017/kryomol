# File generated by kdevelop's qmake manager. 
# ------------------------------------------- 
# Subdir relative project main directory: ./plugin
# Target is a library:  qryomolplugin

configfile = ../config/qryomol.pri
include($${configfile} )

TEMPLATE = lib

QT += opengl

CONFIG +=  warn_on qt opengl static

!static {
DEFINES += QRYOMOLDLL
} else {
DEFINES += QRYOMOLLIB
}
TARGET = qryomolplugin

HEADERS += tools/qhiddendockwindow.h \
           tools/url.h \
           tools/qcwtcombobox.h \
	   tools/qnumlistviewitem.h \
           tools/qlogtablewidget.h \
           tools/exception.h \
           tools/qlogtable.h \
           world/glvisorbase.h \
	   world/glvisor.h \
           world/world.h \
	   world/handler.h \
           plugin.h \
           export.h \
           pluginloader.h \ 
           qryomolapp.h \
           world/kryovisor.h \
           tools/qtimeropenmenu.h \
           tools/qrenumberoptionsdialog.h \
           world/kryovisoroptical.h
SOURCES += tools/qhiddendockwindow.cpp \
           tools/url.cpp \
           tools/qcwtcombobox.cpp \
           tools/qnumlistviewitem.cpp \
           tools/qlogtablewidget.cpp \
           tools/qlogtable.cpp \
           world/glvisorbase.cpp \
	   world/glvisor.cpp \
           world/world.cpp \
	   world/handler.cpp \
           plugin.cpp \
           pluginloader.cpp \
           qryomolapp.cpp \
           world/kryovisor.cpp \
           tools/qtimeropenmenu.cpp \
           tools/qrenumberoptionsdialog.cpp \
           world/kryovisoroptical.cpp


LIBS += -lqryomolcore -lqryomoltools -lqryomolrender -lqryomolgl2ps -lqryomolparsers

#necessary from snow leopard
macx {
LIBS += -framework Carbon
}

INCLUDEPATH += ./ ../tools \
../core ../render ./world ./tools  \
../parsers \
../src ../tools ../3dparty/gl2ps ../icons .a

cuda {
INCLUDEPATH += ../cudatools
}


win32{
  DLLDESTDIR = "$$(KRYOMOL_DIR)/bin"
  CONFIG(debug, debug|release){
    LIBS += -L../core/debug -L../tools/debug/ -L/../render/debug -L../3dparty/gl2ps/debug -lshell32
  }
  CONFIG(release, debug|release){
    LIBS += -L../core/release -L../tools/release -L../render/release -L../3dparty/gl2ps/release -lshell32
  }
}
unix{
  LIBS += -L../core -L../tools -L../render -L../3dparty/gl2ps -L../parsers
}

#INSTALLS += headers target

headers.files += $$HEADERS
!macx {
target.path = $$(KRYOMOLDIR)/lib
headers.path = "$$(KRYOMOL_DIR)/include/kryomol"
} else {
QMAKE_LFLAGS_SONAME  = -Wl,-install_name,@executable_path/../lib/
target.path = "$$(KRYOMOL_DIR)/$${APPNAME}.app/Contents/lib"
headers.path = "$$(KRYOMOL_DIR)/$${APPNAME}.app/Contents/Headers/kryomol"
}
	    
