# File generated by kdevelop's qmake manager. 
# ------------------------------------------- 
# Subdir relative project main directory: ./density
# Target is a library:
configfile = ../config/qryomol.pri
include($${configfile} )

TEMPLATE = lib

QT += opengl

CONFIG += warn_on qt opengl static

!static {
DEFINES += QRYOMOLDLL
} else {
DEFINES += QRYOMOLLIB
}

TARGET = qryomolrender

HEADERS += renderorbitals.h \
    density.h \
    renderexport.h

SOURCES += renderorbitals.cpp density.cpp

INCLUDEPATH += ../tools \
               ../core

cuda {
INCLUDEPATH += ../cudatools
}

LIBS += -lqryomoltools -lqryomolcore

win32{
  DLLDESTDIR = "$$(KRYOMOL_DIR)/bin"
  CONFIG(debug,debug|release) {
    LIBS += -L../tools/debug -L../core/debug
  }
  CONFIG(release,debug|release) {
    LIBS += -L../tools/release -L../core/release
  }
}
unix{
  LIBS += -L../tools -L../core
}

macx {
 QMAKE_LFLAGS_SONAME  = -Wl,-install_name,@executable_path/../lib/
 #QMAKE_CXXFLAGS = -mmmx -mfpmath=sse -msse -msse2 -msse3
}

headers.files = $$HEADERS
!macx {
    headers.path = "$$(KRYOMOL_DIR)/include/kryomol"
    target.path = $$(KRYOMOL_DIR)/lib
}else {
    QMAKE_LFLAGS_SONAME  = -Wl,-install_name,@executable_path/../lib/
    headers.path = $$(KRYOMOL_DIR)/$${APPNAME}.app/Contents/Headers/kryomol
    target.path = $$(KRYOMOL_DIR)/$${APPNAME}.app/Contents/lib
}

#INSTALLS += headers target
